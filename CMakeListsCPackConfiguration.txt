# ---------------------------------------------------------------------------
# Self-contained install configuration
# ---------------------------------------------------------------------------
set(CPACK_PACKAGE_NAME "waveshare-modbus-commander")

if(CMAKE_SYSTEM_NAME STREQUAL "Linux" AND WAVESHARE_ENABLE_CPACK)
    # Install to private prefix under /opt
    set(INSTALL_PREFIX "/opt/daixtrose/${CPACK_PACKAGE_NAME}-${PROJECT_VERSION}")
    set(CMAKE_INSTALL_PREFIX ${INSTALL_PREFIX} CACHE PATH "" FORCE)
    set(CPACK_PACKAGING_INSTALL_PREFIX ${INSTALL_PREFIX})

    # RUNPATH: executable finds shared libs relative to its own location
    file(RELATIVE_PATH _libdir_rel
        ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_INSTALL_BINDIR}
        ${CMAKE_CURRENT_BINARY_DIR}/${CMAKE_INSTALL_LIBDIR}
    )
    set_target_properties(waveshare_modbus_commander PROPERTIES
        BUILD_RPATH_USE_ORIGIN ON
        INSTALL_RPATH "$ORIGIN:$ORIGIN/${_libdir_rel}"
    )
endif()

install(TARGETS waveshare_modbus_commander
    RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
)

# Ship the libmodbus shared library (Linux only; on Windows, libmodbus is linked statically)
if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
    install(DIRECTORY "${CMAKE_BINARY_DIR}/_deps/libmodbus-install/lib/"
        DESTINATION ${CMAKE_INSTALL_LIBDIR}
        FILES_MATCHING
        PATTERN "libmodbus.so*"
        PATTERN "pkgconfig" EXCLUDE
    )
endif()

if(WAVESHARE_ENABLE_CPACK)
    set(CPACK_PACKAGE_VENDOR "Markus Werle")
    set(CPACK_PACKAGE_DESCRIPTION_SUMMARY "${PROJECT_DESCRIPTION}")
    set(CPACK_PACKAGE_CONTACT "daixtrose")
    set(CPACK_DEBIAN_PACKAGE_MAINTAINER "Markus Werle")
    set(CPACK_PACKAGE_VERSION ${PROJECT_VERSION})
    set(CPACK_VERBATIM_VARIABLES YES)

    # Standard naming with architecture: <pkg>_<ver>_<arch>.deb / <pkg>-<ver>-<rel>.<arch>.rpm
    set(CPACK_DEBIAN_FILE_NAME DEB-DEFAULT)
    set(CPACK_RPM_FILE_NAME RPM-DEFAULT)
    set(CPACK_RPM_PACKAGE_RELEASE "1")

    # Override RPM strip command for cross-compilation
    if(NOT "${CMAKE_SYSTEM_PROCESSOR}" STREQUAL "${CMAKE_HOST_SYSTEM_PROCESSOR}")
        set(CPACK_RPM_SPEC_MORE_DEFINE "%define __strip ${CMAKE_STRIP}")
        # Explicit architecture for cross-compiled packages
        if(CMAKE_SYSTEM_PROCESSOR STREQUAL "aarch64")
            set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "arm64")
            set(CPACK_RPM_PACKAGE_ARCHITECTURE "aarch64")
        else()
            set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "${CMAKE_SYSTEM_PROCESSOR}")
            set(CPACK_RPM_PACKAGE_ARCHITECTURE "${CMAKE_SYSTEM_PROCESSOR}")
        endif()
        # Include architecture in TGZ filename for disambiguation
        set(CPACK_PACKAGE_FILE_NAME "${CPACK_PACKAGE_NAME}-${PROJECT_VERSION}-linux-${CMAKE_SYSTEM_PROCESSOR}")
    endif()

    if(CMAKE_SYSTEM_NAME STREQUAL "Linux")
        set(CPACK_GENERATOR TGZ DEB RPM)

        # Debian maintainer scripts: symlink into /usr/local/bin
        set(EXECUTABLE_NAME "waveshare_modbus_commander")
        configure_file(
            ${CMAKE_CURRENT_LIST_DIR}/packaging/postinst.in
            ${CMAKE_CURRENT_BINARY_DIR}/packaging/postinst
            @ONLY
            FILE_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE
                             GROUP_READ GROUP_EXECUTE
                             WORLD_READ WORLD_EXECUTE
        )
        configure_file(
            ${CMAKE_CURRENT_LIST_DIR}/packaging/prerm.in
            ${CMAKE_CURRENT_BINARY_DIR}/packaging/prerm
            @ONLY
            FILE_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE
                             GROUP_READ GROUP_EXECUTE
                             WORLD_READ WORLD_EXECUTE
        )
        list(APPEND CPACK_DEBIAN_PACKAGE_CONTROL_EXTRA
            "${CMAKE_CURRENT_BINARY_DIR}/packaging/postinst"
            "${CMAKE_CURRENT_BINARY_DIR}/packaging/prerm"
        )
    elseif(WIN32)
        set(CPACK_GENERATOR ZIP)
    else()
        set(CPACK_GENERATOR TGZ)
    endif()

    include(CPack)
endif()
